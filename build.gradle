apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'groovy'

repositories {
    jcenter()
    mavenCentral()
}

sourceSets {
    selenium
    seleniumXvfb
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.5'
    testCompile 'junit:junit:4.11'

    compile 'org.codehaus.groovy:groovy-all:2.3.6'
    compile 'com.sparkjava:spark-template-freemarker:1.0'
    compile 'com.sparkjava:spark-core:1.1.1'

    seleniumCompile 'junit:junit:4.11'
    seleniumCompile 'org.seleniumhq.selenium:selenium-java:2.39.0'

    seleniumXvfbCompile 'junit:junit:4.11'
    seleniumXvfbCompile 'org.seleniumhq.selenium:selenium-java:2.39.0'
}

mainClassName = "is.stodid.tictactoe.TicWeb"

task selenium(type: Test, dependsOn: installApp) {
    testClassesDir = sourceSets.selenium.output.classesDir
    classpath = sourceSets.selenium.runtimeClasspath

    ant.condition(property: "os", value: "windows") { os(family: "windows") }
    ant.condition(property: "os", value: "unix"   ) { os(family: "unix")    }

    doFirst {
        switch(ant.properties.os) {
            case 'unix':
                'build/install/late-term-assignment/bin/late-term-assignment &'.execute()
                break
            case 'windows':
                'build\\install\\late-term-assignment\\bin\\late-term-assignment.bat'.execute()
                break
        }
    }
}

task seleniumXvfb(type: Test, dependsOn: installApp) {
    testClassesDir = sourceSets.selenium.output.classesDir
    classpath = sourceSets.selenium.runtimeClasspath

    environment "DISPLAY", ":99"

    doFirst {
        'bin/xvfb start'.execute()
        'build/install/late-term-assignment/bin/late-term-assignment &'.execute()
    }
    
    doLast {
        'bin/xvfb stop'.execute()
    }
}

task distribution << {
    println "We build the zip with version=$version"
}

task release(dependsOn: 'distribution') << {
    println 'We release now'
}

gradle.taskGraph.whenReady {taskGraph ->
    if (taskGraph.hasTask(release)) {
        version = '1.0'
    } else {
        version = '1.0-SNAPSHOT'
    }
}

jar {
    baseName = 'app'
    version = '0.1'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

task stage { 
    dependsOn installApp 
}
